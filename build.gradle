plugins {
    id 'com.gradle.plugin-publish' version '1.3.1'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'signing'
    id 'net.researchgate.release' version '3.1.0'
    id 'ru.vyarus.quality' version '6.0.0'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'ru.vyarus.java-lib' version '3.0.0'
    id 'ru.vyarus.github-info' version '2.0.0'
    id 'com.github.ben-manes.versions' version '0.52.0'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.12"
//    id 'org.ysb33r.gradletest' version '4.1.0'
    id 'ru.vyarus.mkdocs' version '4.0.1'
}

java {
    sourceCompatibility = 1.8
}

wrapper {
    gradleVersion = '8.14.3'
    distributionType = Wrapper.DistributionType.BIN
}

ext {
    animalsnifferVersion = '2.0.1'
    cpdVersion = '3.5'
    spotbugsPluginVersion = '6.2.6'
    docVersion = '6.0.1'
}

repositories { mavenLocal(); mavenCentral(); gradlePluginPortal() }
dependencies {
    testImplementation ("org.spockframework:spock-core:2.4-M6-groovy-3.0") {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation "ru.vyarus:gradle-animalsniffer-plugin:$animalsnifferVersion"
    testImplementation "de.aaschmid:gradle-cpd-plugin:$cpdVersion"
    // spotbugs plugin not included because both 5.x and 6.x versions must be tested
    // testImplementation "com.github.spotbugs.snom:spotbugs-gradle-plugin:$spotbugsPluginVersion"

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

group = 'ru.vyarus'
description = 'Gradle quality plugin for Java and Groovy'

github {
    user = 'xvik'
    license = 'MIT'
}

mkdocs {
    publish {
        docPath = project.docVersion
        rootRedirect = true
        rootRedirectTo = 'latest'
        versionAliases = ['latest']
        hideOldBugfixVersions = true
    }
    extras = [
            'version': project.docVersion,
            'checkstyle': '11.0.1',
            'pmd': '7.16.0',
            'spotbugs': '4.9.4',
            'codenarc': '3.6.0',
            'spotbugsPlugin': project.spotbugsPluginVersion,
            'cpd': project.cpdVersion,
            'animalsnifferPlugin': project.animalsnifferVersion,
    ]
}

maven.pom {
    developers {
        developer {
            id = 'xvik'
            name = 'Vyacheslav Rusakov'
            email = 'vyarus@gmail.com'
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl = uri("https://ossrh-staging-api.central.sonatype.com/service/local/")
            snapshotRepositoryUrl = uri("https://central.sonatype.com/repository/maven-snapshots/")
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

// skip signing for jitpack (snapshots)
tasks.withType(Sign).configureEach { onlyIf { !System.getenv('JITPACK') } }

// Required signing properties for release: signing.keyId, signing.password and signing.secretKeyRingFile
// (https://docs.gradle.org/current/userguide/signing_plugin.html#sec:signatory_credentials)

javaLib {
    // don't publish gradle metadata artifact
    withoutGradleMetadata()
}


gradlePlugin {
    plugins {
        qualityPlugin {
            id = 'ru.vyarus.quality'
            displayName = project.description
            description = 'Static code analysis for Java and Groovy projects'
            tags.set(['java', 'groovy', 'quality', 'checkstyle', 'pmd', 'cpd', 'spotbugs', 'codenarc'])
            implementationClass = 'ru.vyarus.gradle.plugin.quality.QualityPlugin'
        }
    }
}

release.git.requireBranch.set('master')

afterReleaseBuild {
    dependsOn = [
            'publishMavenPublicationToSonatypeRepository',
            'closeAndReleaseSonatypeStagingRepository',
            publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events 'skipped', 'failed'
        exceptionFormat = 'full'
    }
    maxHeapSize = '2g'
    doLast {
        sleep(1000)
    }
}

// run sample projects in src/gradleTest with different gradle versions
//gradleTestSets {
//    testSets {
//        main {
//            // earlier gradle versions (< 8.4) can't be used due to a bug in failing tests
//            versions '8.4', '9.0.0'
//            defaultTaskToRun = 'check'
//        }
//    }
//
//}

// use java 11 to test gradle 7.1 and 17 for gradle 8 and 9
//gradleTest.onlyIf { JavaVersion.current() >= JavaVersion.VERSION_11 && !System.getenv('APPVEYOR')}

dependencyUpdates.revision = 'release'
