plugins {
    id 'com.gradle.plugin-publish' version '0.14.0'
    id 'java-gradle-plugin'
    id 'groovy'
    id 'jacoco'
    id 'signing'
    id 'net.researchgate.release' version '2.8.1'
    id 'ru.vyarus.quality' version '4.5.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
    id 'ru.vyarus.java-lib' version '2.1.0'
    id 'ru.vyarus.github-info' version '1.2.0'
    id 'com.github.ben-manes.versions' version '0.38.0'
    id "pl.droidsonroids.jacoco.testkit" version "1.0.8"
    id 'ru.vyarus.mkdocs' version '2.1.1'
}

sourceCompatibility = 1.8

wrapper {
    gradleVersion = '6.3'
}

ext {
    animalsnifferVersion = '1.5.3'
    cpdVersion = '3.2'
    spotbugsPluginVersion = '4.7.0'
    docVersion = '4.6.0'
}

repositories { mavenCentral(); gradlePluginPortal() }
dependencies {
    implementation "gradle.plugin.com.github.spotbugs.snom:spotbugs-gradle-plugin:$spotbugsPluginVersion"

    testImplementation('org.spockframework:spock-core:1.3-groovy-2.5') {
        exclude group: 'org.codehaus.groovy'
    }
    testImplementation "ru.vyarus:gradle-animalsniffer-plugin:$animalsnifferVersion"
    testImplementation "de.aaschmid:gradle-cpd-plugin:$cpdVersion"
}

group = 'ru.vyarus'
description = 'Gradle quality plugin for Java and Groovy'

github {
    user 'xvik'
    license 'MIT'
}

mkdocs {
    publish {
        docPath = project.docVersion
        rootRedirect = true
    }
    extras = [
            'version': project.docVersion,
            'checkstyle': '8.42',
            'pmd': '6.34.0',
            'spotbugs': '4.2.3',
            'codenarc': '2.1.0',
            'spotbugsPlugin': project.spotbugsPluginVersion,
            'cpd': project.cpdVersion,
            'animalsnifferPlugin': project.animalsnifferVersion,
    ]
}

pom {
    developers {
        developer {
            id 'xvik'
            name 'Vyacheslav Rusakov'
            email 'vyarus@gmail.com'
        }
    }
}

gradlePlugin {
    plugins {
        qualityPlugin {
            id = 'ru.vyarus.quality'
            displayName = project.description
            implementationClass = 'ru.vyarus.gradle.plugin.quality.QualityPlugin'
        }
    }
}
pluginBundle {
    description = 'Static code analysis for Java and Groovy projects'
    tags = ['java', 'groovy', 'quality', 'checkstyle', 'pmd', 'cpd', 'spotbugs', 'codenarc']

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = findProperty('sonatypeUser')
            password = findProperty('sonatypePassword')
        }
    }
}

signing {
    // properties must be specified in ~/.gradle/gradle.properties
    // signing.keyId=24875D73
    // signing.password=secret
    // signing.secretKeyRingFile=/Users/me/.gnupg/secring.gpg

    sign publishing.publications.maven
}

afterReleaseBuild {
    dependsOn = [
            signMavenPublication,
            'publishToSonatype',
            'closeAndReleaseSonatypeStagingRepository',
            publishPlugins]
    doLast {
        logger.warn "RELEASED $project.group:$project.name:$project.version"
    }
}

test {
    testLogging {
        events 'skipped', 'failed', 'standard_error'
        exceptionFormat 'full'
    }
    maxHeapSize = '2g'
    forkEvery = 4
    doLast {
        sleep(1000)
    }
}

dependencyUpdates.revision = 'release'
jacocoTestReport.reports.xml.enabled = true
